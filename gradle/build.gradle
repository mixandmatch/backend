//usage: gradlew -q -Penv=[dev|test|prod] printProps
apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'eclipse'

loadConfiguration()

repositories { mavenCentral() }

dependencies {
    compile "joda-time:joda-time:2.2"
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}

task printProps << {
    println "serverName:  $config.serverName"
    println "mail.server: $config.mail.server"
    println "mail.port:   $config.mail.port"
}

def loadConfiguration() {
    def environment = hasProperty('env') ? env : 'dev'
    setProperty 'environment', environment
    println "Environment is set to $environment"
    
    def configFile = file('projectSettings.groovy')
    def config = new ConfigSlurper(environment).parse(configFile.toURL())
    setProperty 'config', config
}